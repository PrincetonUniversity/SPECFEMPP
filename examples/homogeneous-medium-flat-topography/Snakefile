
rule all:
    input:
        plot = "OUTPUT_FILES/results/plot.png"
    localrule: True

rule generate_mesh:
    input: "Par_File"
    output:
        database = "OUTPUT_FILES/database.bin",
        stations = "OUTPUT_FILES/STATIONS"
    shell:
        '''
            mkdir -p OUTPUT_FILES
            ./../../build/xmeshfem2D -p {input}
        '''

rule run_solver:
    input:
        database = "OUTPUT_FILES/database.bin",
        stations = "OUTPUT_FILES/STATIONS",
        source = "source.yaml",
        config = "specfem_config.yaml"

    output:
        siesmograms = expand("OUTPUT_FILES/results/{station_name}{network_name}{component}.semv",
            station_name=["S0001", "S0002", "S0003", "S0004", "S0005", "S0006"],
            network_name=["AA"],
            component=["BXX", "BXZ"])

    shell:
        '''
            module purge
            module load boost/1.73.0
            echo "Hostname: $(hostname)" > output.log
            ./../../build/specfem2d -p {input.config} >> output.log
        '''

rule plot_seismogram:
    input:
        trace_files = expand("OUTPUT_FILES/results/{station_name}{network_name}{component}.semv",
            station_name=["S0001", "S0002", "S0003", "S0004", "S0005", "S0006"],
            network_name=["AA"],
            component=["BXX", "BXZ"])
    output:
        traces = "OUTPUT_FILES/results/plot.png"
    run:
        import glob
        import os
        import numpy as np
        import obspy

        def get_traces(directory):
            traces = []
            files = glob.glob(directory + "/*.sem*")
            ## iterate over all seismograms
            for filename in files:
                station_name = os.path.splitext(filename)[0]
                station_name = station_name.split('/')[-1]
                trace = np.loadtxt(filename, delimiter=' ')
                starttime = trace[0,0]
                dt = trace[1,0] - trace[0,0]
                traces.append(obspy.Trace(trace[:,1], {'network': station_name, 'starttime': starttime, 'delta': dt}))

            stream = obspy.Stream(traces)

            return stream


        stream = get_traces("OUTPUT_FILES/results")
        stream.plot(size=(800, 1000)).savefig(output.traces)



rule clean:
    shell:
        '''
            rm -rf OUTPUT_FILES
        '''
